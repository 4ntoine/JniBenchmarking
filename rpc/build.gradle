plugins {
  id 'com.android.library'
  id 'kotlin-android'

  // protobuf
   id 'com.google.protobuf'
//  id 'com.squareup.wire'
}

android {
  compileSdkVersion 29
  buildToolsVersion "30.0.2"

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 29
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
  }
  // configuration for protobuf-lite
  sourceSets {
    main {
      // for IDE to find them
      java {
        srcDirs 'build/generated/source/proto/debug/java'
        srcDirs 'build/generated/source/proto/debug/grpc'
      }

      proto {
        srcDir 'src/main/proto_lite'
      }
    }
  }
}

def grpcVersion = "1.34.0"
def protocVersion = "3.14.0"

dependencies {
  api("com.google.protobuf:protobuf-javalite:$protocVersion") { // lite for protobuf
    exclude module: "protobuf-lite"
  }
//  api "com.squareup.wire:wire-runtime:3.5.0" // wire for protobuf
  api "io.grpc:grpc-protobuf-lite:$grpcVersion"
  api "io.grpc:grpc-core:$grpcVersion"
  api "io.grpc:grpc-stub:$grpcVersion"
  api "io.grpc:grpc-auth:$grpcVersion"
  implementation 'javax.annotation:javax.annotation-api:1.3.2'

  testImplementation 'junit:junit:4.13.1'
  testImplementation "io.grpc:grpc-netty-shaded:${grpcVersion}"

  androidTestImplementation 'androidx.test.ext:junit:1.1.2'
  androidTestImplementation 'androidx.test:runner:1.3.0'
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protocVersion"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
    javalite {
      artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {
          option "lite"
        }
      }

      task.plugins {
        grpc {
          option 'lite'
        }
      }
    }
  }
}

// wire {
//   sourcePath {
//     srcDir 'src/main/proto_wire'
//   }

//   kotlin { } // or `java {}`
// }